.data
  N:      .dword 4096          # Tamaño de los vectores
  Alpha:  .dword 2             # Escalar a multiplicar

.bss
.align 3
  X: .zero 32768               # Vector X (4096)*8 bytes
  Y: .zero 32768               # Vector Y (4096)*8 bytes
  Z: .zero 32768               # Vector Z (4096)*8 bytes

.section .text
.global main

# Direcciones CSR
.equ CSR_MCOUNTINHIBIT, 0x320
.equ CSR_MHPMEVENT3,    0x323
.equ CSR_MHPMEVENT4,    0x324
.equ CSR_MHPMEVENT5,    0x325
.equ CSR_MHPMEVENT6,    0x326
.equ CSR_MHPMEVENT7,    0x327
.equ CSR_MHPMEVENT8,    0x328

.equ CSR_MCYCLE,        0xB00
.equ CSR_MINSTRET,      0xB02
.equ CSR_MHPMCOUNTER3,  0xB03
.equ CSR_MHPMCOUNTER4,  0xB04
.equ CSR_MHPMCOUNTER5,  0xB05
.equ CSR_MHPMCOUNTER6,  0xB06
.equ CSR_MHPMCOUNTER7,  0xB07
.equ CSR_MHPMCOUNTER8,  0xB08

# Constantes de cálculo
.equ CPU_FREQ,          50000000  # 50 MHz

main:
  # ---------------------------------------------------------
  # CONFIGURACIÓN DEL PMU (PERFORMANCE MONITOR UNIT)
  # --------------------------------------------------------- 
  # Pausar todos los contadores
  addi    sp, sp, -80
  li      t0, -1
  csrw    CSR_MCOUNTINHIBIT, t0

  # Configurar Eventos
  li      t0, 1
  csrw    CSR_MHPMEVENT3, t0    # L1 I-Cache Miss
  li      t0, 2
  csrw    CSR_MHPMEVENT4, t0    # L1 D-Cache Miss
  li      t0, 16
  csrw    CSR_MHPMEVENT5, t0    # L1 I-Cache Access
  li      t0, 17
  csrw    CSR_MHPMEVENT6, t0    # L1 D-Cache Access
  li      t0, 9
  csrw    CSR_MHPMEVENT7, t0    # Branch Instr
  li      t0, 10
  csrw    CSR_MHPMEVENT8, t0    # Branch Mispredict

  # Resetear contadores a 0
  csrw    CSR_MHPMCOUNTER3, zero
  csrw    CSR_MHPMCOUNTER4, zero
  csrw    CSR_MHPMCOUNTER5, zero
  csrw    CSR_MHPMCOUNTER6, zero
  csrw    CSR_MHPMCOUNTER7, zero
  csrw    CSR_MHPMCOUNTER8, zero

  # Habilitar acceso a contadores
  li      t0, -1
  csrw    mcounteren, t0
  csrw    CSR_MCOUNTINHIBIT, zero

  # ---------------------------------------------------------
  # SNAPSHOT INICIAL
  # ---------------------------------------------------------
  csrr    t0, CSR_MCYCLE
  sd      t0, 0(sp)       # Guardar Cycle Start
  csrr    t0, CSR_MINSTRET
  sd      t0, 8(sp)       # Guardar Instret Start
  csrr    t0, CSR_MHPMCOUNTER3
  sd      t0, 16(sp)      # Guardar HPM3 Start
  csrr    t0, CSR_MHPMCOUNTER4
  sd      t0, 24(sp)      # Guardar HPM4 Start
  csrr    t0, CSR_MHPMCOUNTER5
  sd      t0, 32(sp)      # Guardar HPM5 Start
  csrr    t0, CSR_MHPMCOUNTER6
  sd      t0, 40(sp)      # Guardar HPM6 Start
  csrr    t0, CSR_MHPMCOUNTER7
  sd      t0, 48(sp)      # Guardar HPM7 Start
  csrr    t0, CSR_MHPMCOUNTER8
  sd      t0, 56(sp)      # Guardar HPM8 Start

  # ---------------------------------------------------------
  # CODIGO
  # ---------------------------------------------------------
  la t0, N
  ld t1, 0(t0)                 # t1 = N (4096)
  
  la t2, Alpha
  ld t3, 0(t2)
  fcvt.d.l f10, t3             # f10 = Alpha (double)

  la s2, X
  la s3, Y
  la s4, Z

  mv s5, s2                    # Puntero X
  mv s6, s3                    # Puntero Y
  mv s7, s4                    # Puntero Z

  srli t5, t1, 2               # t5 = N >> 2 (división por 4)
  
  li t4, 0                     # t4 = contador (i = 0)

loop:
  # BLOQUE 1 (Offset 0)
  fld f5, 0(s5)
  fld f6, 0(s6)
  fmul.d f7, f10, f5
  fadd.d f8, f7, f6
  fsd f8, 0(s7)

  # BLOQUE 2 (Offset 8)
  fld f9, 8(s5)
  fld f11, 8(s6)
  fmul.d f12, f10, f9
  fadd.d f13, f12, f11
  fsd f13, 8(s7)

  # BLOQUE 3 (Offset 16)
  fld f14, 16(s5)
  fld f15, 16(s6)
  fmul.d f16, f10, f14
  fadd.d f17, f16, f15
  fsd f17, 16(s7)

  # BLOQUE 4 (Offset 24)
  fld f18, 24(s5)
  fld f19, 24(s6)
  fmul.d f20, f10, f18
  fadd.d f21, f20, f19
  fsd f21, 24(s7)

  addi s5, s5, 32              # Avanzar 4 doubles (32 bytes) en X
  addi s6, s6, 32              # Avanzar 4 doubles (32 bytes) en Y
  addi s7, s7, 32              # Avanzar 4 doubles (32 bytes) en Z

  # CONTROL DEL BUCLE
  addi t4, t4, 1               # i++
  blt t4, t5, loop

  # ---------------------------------------------------------
  # SNAPSHOT FINAL Y CÁLCULO DE DIFERENCIAS
  # ---------------------------------------------------------
  # x18 (s2) = Cycles
  csrr    s2, CSR_MCYCLE
  ld      t0, 0(sp)
  sub     s2, s2, t0

  # x19 (s3) = Instructions
  csrr    s3, CSR_MINSTRET
  ld      t0, 8(sp)
  sub     s3, s3, t0

  # x20 (s4) = I-Cache Miss
  csrr    s4, CSR_MHPMCOUNTER3
  ld      t0, 16(sp)
  sub     s4, s4, t0

  # x21 (s5) = D-Cache Miss
  csrr    s5, CSR_MHPMCOUNTER4
  ld      t0, 24(sp)
  sub     s5, s5, t0

  # x22 (s6) = I-Cache Access
  csrr    s6, CSR_MHPMCOUNTER5
  ld      t0, 32(sp)
  sub     s6, s6, t0

  # x23 (s7) = D-Cache Access
  csrr    s7, CSR_MHPMCOUNTER6
  ld      t0, 40(sp)
  sub     s7, s7, t0

  # x24 (s8) = Branches
  csrr    s8, CSR_MHPMCOUNTER7
  ld      t0, 48(sp)
  sub     s8, s8, t0

  # x25 (s9) = Branch Miss
  csrr    s9, CSR_MHPMCOUNTER8
  ld      t0, 56(sp)
  sub     s9, s9, t0

  # Restaurar Stack Pointer
  addi    sp, sp, 80

  # ---------------------------------------------------------
  # CÁLCULO TIEMPO (us) -> x26 (s10)
  # ---------------------------------------------------------
  li      t0, 1000000
  mul     s10, s2, t0      # s10 = Cycles (s2) * 1M
  li      t0, CPU_FREQ
  divu    s10, s10, t0     # s10 = s10 / Freq

  # ---------------------------------------------------------
  # SALIDA
  # ---------------------------------------------------------
  li      a0, 0
  jal     exit